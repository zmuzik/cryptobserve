apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"

    Properties customProps = new Properties()
    customProps.load(new FileInputStream("../keystore/properties"))

    defaultConfig {
        applicationId "zmuzik.cryptobserve"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }

    signingConfigs {
        zm {
            storeFile file("../../keystore/keystore")
            keyAlias customProps.KEY_ALIAS
            storePassword customProps.STORE_PASSWORD
            keyPassword customProps.KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix '-debug'
            minifyEnabled false
        }

        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix '-dev'
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.zm
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.zm
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kapt {
    generateStubs = true
}

ext {
    supportLibVersion = "26.1.0"
    archComponentsVersion = "1.0.0-alpha9"
    daggerVersion = "2.11"
    retrofitVersion = "2.3.0"
    rxJavaVersion = "2.1.1"
    rxAndroidVersion = "2.0.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //support lib
    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // lifecycles, livedata, viewmodel
    implementation "android.arch.lifecycle:runtime:$archComponentsVersion"
    implementation "android.arch.lifecycle:extensions:$archComponentsVersion"
    annotationProcessor "android.arch.lifecycle:compiler:$archComponentsVersion"

    // room
    implementation "android.arch.persistence.room:runtime:$archComponentsVersion"
    implementation "android.arch.persistence.room:rxjava2:$archComponentsVersion"
    kapt "android.arch.persistence.room:compiler:$archComponentsVersion"

    //dagger
    kapt "com.google.dagger:dagger-compiler:$project.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$project.daggerVersion"
    implementation "com.google.dagger:dagger:$project.daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$project.daggerVersion"

    //gson
    implementation 'com.google.code.gson:gson:2.8.0'

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:3.8.1"

    // rx java
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // timber
    implementation 'com.jakewharton.timber:timber:4.5.1'

    //testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
